#!/usr/bin/env zsh
# vim: set ft=zsh:

# {{- $cmd := joinPath .chezmoi.sourceDir "support/checksum.sh" }}
# {{- $pkgs := joinPath .chezmoi.sourceDir "packages.yaml" }}
# {{- $upkgs := joinPath .chezmoi.homeDir ".config/bmatheny/packages.yaml" }}

# packages.yaml hash: {{ output $cmd $pkgs | trim }}
# ~/.config/bmatheny/packages.yaml hash: {{ output $cmd $upkgs | trim }}

function error_fn {
  echo "Exiting with error: '$1'"
  exit 1
}

if (( ! $+commands[brew] )); then
  homebrew_locations=(
    "${HOME}/homebrew"
    "/usr/local")
  for location in $homebrew_locations; do
    if [[ -d "$location/bin" && -x "$location/bin/brew" ]]; then
      eval "$(${location}/bin/brew shellenv)"
      break
    fi
  done
fi

(( $+commands[brew] )) || error_fn "Could not find homebrew"
(( $+commands[chezmoi] )) || error_fn "Could not find chezmoi"
(( $+commands[mas] )) || error_fn "Could not find mas"
(( $+commands[shyaml] )) || error_fn "Could not find shyaml"

packages="$(chezmoi source-path)/packages.yaml"
[[ -f $packages ]] || error_fn "Could not find packages file '${packages}'"

function install_apps_from_file {
  local file="$1"
  local IFS=$'\n'
  if [[ -f "${file}" ]]; then
    for line in $(cat "${file}" | shyaml -y -q get-values mas); do
      local appname=$(echo "${line}" | shyaml keys)
      local appid=$(echo "${line}" | shyaml values)
      echo "Installing or upgrading '${appname}'"
      mas install "${appid}" 2>/dev/null # Will silently do nothing if already installed
      mas upgrade "${appid}" 2>/dev/null # Will silently do nothing if already installed
    done
  fi
}

install_apps_from_file "$packages"
install_apps_from_file "$HOME/.config/bmatheny/packages.yaml"

exit 0
